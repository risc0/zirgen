// RUN: zirgen -I %S --test %s
// Bit packing and unpacking logic

import bits;
import arr;

// The max we can pack into one BB element is 30 bits
#[picus_inline]
component Po2(n: Val) {
  arr := [ 0x00000001, 0x00000002, 0x00000004, 0x00000008,
           0x00000010, 0x00000020, 0x00000040, 0x00000080,
           0x00000100, 0x00000200, 0x00000400, 0x00000800,
           0x00001000, 0x00002000, 0x00004000, 0x00008000,
           0x00010000, 0x00020000, 0x00040000, 0x00080000,
           0x00100000, 0x00200000, 0x00400000, 0x00800000,
           0x01000000, 0x02000000, 0x04000000, 0x08000000,
           0x10000000, 0x20000000
  ];
  arr[n]
}

// We have 3 functions here:
// 1) Pack an array of N bits into N/P elements
// 2) Unpack an array of N bits from N/P elements *without* verifying
// 3) Same as above, but registerize and verify
// We don't handle 'uneven' packings, and rely on external code to make
// sure the 'parts' eveny divide the whole.

// Pack N bits into parts of P bits each
component Pack<N: Val, P: Val>(in : Array<Val, N>) {
  N % P = 0;
  for i : 0..(N / P) {
    reduce for j : 0..P { Po2(j) * in[i*P+ j] } init 0 with Add
  }
}

component UnpackNondet<N: Val, P: Val>(in: Array<Val, N/P>) {
  N % P = 0;
  inv := Inv(P);
  for n : 0..N {
    j := n % P;
    i := (n - j) * inv;
    (in[i] & Po2(j)) / Po2(j)
  }
}

component UnpackReg<N: Val, P: Val>(in: Array<Val, N/P>) {
  bitVals := UnpackNondet<N, P>(in);
  bits := for n : 0..N { NondetBitReg(bitVals[n]) };
  EqArr<N/P>(Pack<N, P>(bits), in);
  bits
}

test PackUnpack {
  bits := UnpackReg<16, 4>([1, 15, 5, 10]);
  EqArr<16>(bits, [1, 0, 0, 0, 1, 1, 1, 1, 1, 0, 1, 0, 0, 1, 0, 1]);
  oneVal := Pack<16, 16>(bits);
  oneVal[0] = 0xa5f1;
}
