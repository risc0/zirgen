//import one_hot;

component s2a(rtuple: RetTuple) {
  a1 := rtuple.arr_e;
  a2 := for i : 0..10 { rtuple.arr_f[i] };
  vConcatenate<22,10>(a1, a2)
}
component s2e(rtuple: RetTuple) {
  e1 := for i : 10..22 { rtuple.arr_f[i] };
  e2 := for i : 0..20 { rtuple.arr_c[i] };
  vConcatenate<12,20>(e1, e2)
}
component s2w(rtuple: RetTuple) {
  w1 := rtuple.arr_a;
  w2 := for i : 0..10 { rtuple.arr_b[i] };
  vConcatenate<22,10>(w1, w2)
}

component w2s(w_: Array<NondetReg, 32>) {
  public a := for i : 0..22 { NondetReg(w_[i]) };
  for i : 0..22 { AliasLayout!(a[i], w_[i]); };

  public b := for i : 22..32 { NondetReg(w_[i]) };
  for i : 22..32 { AliasLayout!(b[i-22], w_[i]); };
}

component ae2s(a_: Array<NondetReg, 32>, e_: Array<NondetReg, 32>) {
  public e := for i : 0..22 { NondetReg(a_[i]) };
  for i : 0..22 { AliasLayout!(e[i], a_[i]); };

  f1 := for i : 22..32 { NondetReg(a_[i]) };
  for i : 22..32 { AliasLayout!(f1[i-22], a_[i]); };
  f2 := for i : 0..12 { NondetReg(e_[i]) };
  for i : 0..12 { AliasLayout!(f2[i], e_[i]); };
  public f := Concatenate<10,12>(f1, f2);

  public c := for i : 12..32 { NondetReg(e_[i]) };
  for i : 12..32 { AliasLayout!(c[i-12], e_[i]); };
}

component IVvals(i: Val) {
  arr := [[0x6a09, 0xe667],
          [0xbb67, 0xae85],
          [0x3c6e, 0xf372],
          [0xa54f, 0xf53a],
          [0x510e, 0x527f],
          [0x9b05, 0x688c],
          [0x1f83, 0xd9ab],
          [0x5be0, 0xcd19]];
  TwoShorts(arr[i][1],arr[i][0])
}

component sha256rc_table<i: Val>() {
  arr := [[0x428a,0x2f98],[0x7137,0x4491],[0xb5c0,0xfbcf],[0xe9b5,0xdba5],
          [0x3956,0xc25b],[0x59f1,0x11f1],[0x923f,0x82a4],[0xab1c,0x5ed5],
          [0xd807,0xaa98],[0x1283,0x5b01],[0x2431,0x85be],[0x550c,0x7dc3],
          [0x72be,0x5d74],[0x80de,0xb1fe],[0x9bdc,0x06a7],[0xc19b,0xf174],
          [0xe49b,0x69c1],[0xefbe,0x4786],[0x0fc1,0x9dc6],[0x240c,0xa1cc],
          [0x2de9,0x2c6f],[0x4a74,0x84aa],[0x5cb0,0xa9dc],[0x76f9,0x88da],
          [0x983e,0x5152],[0xa831,0xc66d],[0xb003,0x27c8],[0xbf59,0x7fc7],
          [0xc6e0,0x0bf3],[0xd5a7,0x9147],[0x06ca,0x6351],[0x1429,0x2967],
          [0x27b7,0x0a85],[0x2e1b,0x2138],[0x4d2c,0x6dfc],[0x5338,0x0d13],
          [0x650a,0x7354],[0x766a,0x0abb],[0x81c2,0xc92e],[0x9272,0x2c85],
          [0xa2bf,0xe8a1],[0xa81a,0x664b],[0xc24b,0x8b70],[0xc76c,0x51a3],
          [0xd192,0xe819],[0xd699,0x0624],[0xf40e,0x3585],[0x106a,0xa070],
          [0x19a4,0xc116],[0x1e37,0x6c08],[0x2748,0x774c],[0x34b0,0xbcb5],
          [0x391c,0x0cb3],[0x4ed8,0xaa4a],[0x5b9c,0xca4f],[0x682e,0x6ff3],
          [0x748f,0x82ee],[0x78a5,0x636f],[0x84c8,0x7814],[0x8cc7,0x0208],
          [0x90be,0xfffa],[0xa450,0x6ceb],[0xbef9,0xa3f7],[0xc671,0x78f2]];
  TwoShorts(arr[i][1],arr[i][0])
}

component TwoShorts(lsbs: Val, msbs: Val) {
  public low := lsbs;
  public high := msbs;
}

component rightRotate<A: Val, N: Val>(a: Array<Val, A>) {
  for i : 0..A {
    wraparound := InRange(0,i,A-N);
    //[wraparound, 1-wraparound] -> (a[i+N], a[i+N-A])
    wraparound*a[i+N] + (1-wraparound)*a[i+N-A]
  }
}

component rightShift<A: Val, N: Val>(a: Array<Val, A>) {
  for i : 0..A {
    wraparound := InRange(0,i,A-N);
    //[wraparound, 1-wraparound] -> (wraparound*a[i+N], 0)
    wraparound*a[i+N] + 0
  }
  //for i : 0..A-N { AliasLayout!( r[i], a[i+N]); };
}

component xor3a<N: Val>(x: Array<Val, N>,
                        y: Array<Val, N>, z: Array<Val, N>) {
  for i : 0..N {
    ( (x[i]*y[i] + (1-x[i])*(1-y[i])) * z[i] ) +
    ( (1-(x[i]*y[i] + (1-x[i])*(1-y[i]))) * (1-z[i]) ) }
}

component maj3<N: Val>(x: Array<Val, N>,
                       y: Array<Val, N>, z: Array<Val, N>) {
  for i : 0..N
    { x[i]*y[i]*(1-z[i]) +
      x[i]*(1-y[i])*z[i] +
      (1-x[i])*y[i]*z[i] + x[i]*y[i]*z[i] }
}

component ch<N: Val>(x: Array<Val, N>,
                     y: Array<Val, N>, z: Array<Val, N>) {
  for i : 0..N { x[i]*y[i] + (1-x[i])*z[i] }
}

component pack2(arr: Array<Val, 32>) {
  low := FromBits<16>(for i : 0..16 { arr[i] });
  high := FromBits<16>(for i : 16..32 { arr[i] });
  TwoShorts(low, high)
}

component pack2rev(arr: Array<Val, 32>) {
  lowest := FromBits<8>(for i : 0..8 { arr[i] });
  midlow := FromBits<8>(for i : 8..16 { arr[i] });
  midhigh := FromBits<8>(for i : 16..24 { arr[i] });
  highest := FromBits<8>(for i : 24..32 { arr[i] });
  TwoShorts(lowest*256+midlow, midhigh*256+highest)
}

component add(a: TwoShorts, b: TwoShorts) {
  TwoShorts(a.low + b.low, a.high + b.high)
}

component computeW(w_2: Array<Val, 32>, w_7: Array<Val, 32>,
                   w_15: Array<Val, 32>, w_16: Array<Val, 32>) {
  s0 := xor3a<32>(rightRotate<32,7>(w_15),
             rightRotate<32,18>(w_15), rightShift<32,3>(w_15));
  s1 := xor3a<32>(rightRotate<32,17>(w_2),
             rightRotate<32,19>(w_2), rightShift<32,10>(w_2));
  pack_s0 := pack2(s0);
  pack_s1 := pack2(s1);
  add(pack2(w_16), add(pack_s0, add(pack2(w_7), pack_s1)))
}

component computeAE(a_: Array<Val, 32>, b_: Array<Val, 32>,
                    c_: Array<Val, 32>, d_: Array<Val, 32>,
                    e_: Array<Val, 32>, f_: Array<Val, 32>,
                    g_: Array<Val, 32>, h_: Array<Val, 32>,
                    w_: Array<Val, 32>, k_rc: TwoShorts) {
  s0 := xor3a<32>(rightRotate<32,2>(a_),
             rightRotate<32,13>(a_), rightRotate<32,22>(a_));
  s1 := xor3a<32>(rightRotate<32,6>(e_),
             rightRotate<32,11>(e_), rightRotate<32,25>(e_));
  pack_ch := pack2(ch<32>(e_, f_, g_));
  stage1 :=
    add(pack2(w_), add(k_rc, add(pack2(h_), add(pack_ch, pack2(s1)))));
  pack_maj := pack2(maj3<32>(a_, b_, c_));
  public aOut := add(stage1, add(pack_maj, pack2(s0)));
  public eOut := add(stage1, pack2(d_));
}

component unpack_with_carry(low: Val, high: Val) {
  low_bits := ToBitsU<19>(low);
  FromBits<19>(low_bits) = low;
  carry := low_bits[16] + 2*low_bits[17] + 4*low_bits[18];
  high_plus_carry := high + carry;
  high_bits := ToBitsU<20>(high_plus_carry);
  FromBits<20>(high_bits) = high_plus_carry;
  low16 := for i : 0..16 { low_bits[i] };
  high16 := for i : 0..16 { high_bits[i] };
  public carrybits := Concatenate<3,4>(
    for i : 16..19 { low_bits[i] },
    for i : 16..20 { high_bits[i] });
  Concatenate<16,16>(low16, high16)
}

component sha256load<I: Val>(rvals: RetTuple,
                             aux_regs: Array<NondetReg, AUXLEN()>) {
  rvprev01 := rvals@1;
  rvprev02 := rvals@2;
  rvprev03 := rvals@3;
  rvprev04 := rvals@4;

  idx := rvprev01.blk;
  within := rvprev01.midx + 1 - idx;
  invWithin := NondetReg(Inv(within));
  within*invWithin = 1;

  flag := rvprev01.rnd;
  rs : Array<NondetReg, 5>;
  [1-flag, flag] -> (
    { ReadMemory(idx, rs@0); },
    { AliasLayout!(Reg(rvprev01.arr_d[RLEN()-3]), rs@0[0]);
      AliasLayout!(Reg(rvprev01.arr_d[RLEN()-2]), rs@0[1]);
      AliasLayout!(NondetReg(0), rs@0[2]);
      AliasLayout!(NondetReg(0), rs@0[3]);
      AliasLayout!(NondetReg(0), rs@0[4]);
    });
  vals := rs@0;      

  nxt := [1-flag, flag] -> ([1,idx], [0,idx+1]);

  inp1 := vals[0];
  high_w := ToBitsU<16>(inp1); //big endian
  FromBits<16>(high_w) = inp1;
  inp2 := vals[1];
  low_w := ToBitsU<16>(inp2);
  FromBits<16>(low_w) = inp2;
  unpacked_w := [low_w[8],low_w[9],low_w[10],low_w[11],
                 low_w[12],low_w[13],low_w[14],low_w[15],
                 low_w[0],low_w[1],low_w[2],low_w[3],
                 low_w[4],low_w[5],low_w[6],low_w[7],
                 high_w[8],high_w[9],high_w[10],high_w[11],
                 high_w[12],high_w[13],high_w[14],high_w[15],
                 high_w[0],high_w[1],high_w[2],high_w[3],
                 high_w[4],high_w[5],high_w[6],high_w[7]];

  compae := computeAE(
    s2a(rvprev01), s2a(rvprev02), s2a(rvprev03), s2a(rvprev04),
    s2e(rvprev01), s2e(rvprev02), s2e(rvprev03), s2e(rvprev04),
    unpacked_w, sha256rc_table<I>());
  new_a := unpack_with_carry(compae.aOut.low, compae.aOut.high);
  new_e := unpack_with_carry(compae.eOut.low, compae.eOut.high);
  Z := Isz(I-15);
  stae := ae2s(new_a, new_e);
  stw := w2s(unpacked_w);
  tmpb := Concatenate<7,5>(
    new_a.carrybits, for i : 0..5 { new_e.carrybits[i]} );
  padb := Concatenate<10,12>(stw.b, tmpb);
  padc := Concatenate<20,2>(stae.c, for i : 5..7 {new_e.carrybits[i]});
  d := Concatenate<RLEN()-6, 6>(for i : 0..RLEN()-6 { NondetReg(0) },
    [invWithin,vals[0],vals[1],vals[2],vals[3],vals[4]]);
  [1-Z, Z] -> (
    RetTuple(stw.a, padb, padc, d, stae.e, stae.f,
             I+1, S2MAJOR_LOAD(), nxt[0], nxt[1], rvprev01.midx, aux_regs),
    RetTuple(stw.a, padb, padc, d, stae.e, stae.f,
             0, S2MAJOR_MIX(), nxt[0], nxt[1], rvprev01.midx, aux_regs))
}

component sha256load_all(rvals: RetTuple) {
  m := OneHotU<AUXLEN()>(rvals@1.minor);
  for i : AUXLEN()-AUXBIN()..AUXLEN() { 0 = m[i] * (1-m[i]); };
  r := m -> (sha256load<0>(rvals, m),
             sha256load<1>(rvals, m),
             sha256load<2>(rvals, m),
             sha256load<3>(rvals, m),
             sha256load<4>(rvals, m),
             sha256load<5>(rvals, m),
             sha256load<6>(rvals, m),
             sha256load<7>(rvals, m),
             sha256load<8>(rvals, m),
             sha256load<9>(rvals, m),
             sha256load<10>(rvals, m),
             sha256load<11>(rvals, m),
             sha256load<12>(rvals, m),
             sha256load<13>(rvals, m),
             sha256load<14>(rvals, m),
             sha256load<15>(rvals, m));
  for i : 0..BLEN() { 0 = r.arr_a[i] * (1-r.arr_a[i]); };
  for i : 0..BLEN() { 0 = r.arr_b[i] * (1-r.arr_b[i]); };
  for i : 0..BLEN() { 0 = r.arr_c[i] * (1-r.arr_c[i]); };
  r
}

component sha256mix<S: Val, I: Val>(rvals: RetTuple,
                                    aux_regs: Array<NondetReg, AUXLEN()>) {
  rvprev01 := rvals@1;
  rvprev02 := rvals@2;
  rvprev03 := rvals@3;
  rvprev04 := rvals@4;
  rvprev07 := rvals@7;
  rvprev15 := rvals@15;
  rvprev16 := rvals@16;

  packed_w := computeW(s2w(rvprev02), s2w(rvprev07), s2w(rvprev15), s2w(rvprev16));
  new_w := unpack_with_carry(packed_w.low, packed_w.high);
  compae := computeAE(s2a(rvprev01), s2a(rvprev02), s2a(rvprev03), s2a(rvprev04),
                      s2e(rvprev01), s2e(rvprev02), s2e(rvprev03), s2e(rvprev04),
                      new_w, sha256rc_table<(S+1)*16+I>());
  new_a := unpack_with_carry(compae.aOut.low, compae.aOut.high);
  new_e := unpack_with_carry(compae.eOut.low, compae.eOut.high);
  stae := ae2s(new_a, new_e);
  stw := w2s(new_w);
  tmpb := Concatenate<7,5>(
    new_a.carrybits, for i : 0..5 {new_e.carrybits[i]});
  padb := Concatenate<10,12>(stw.b, tmpb);
  padc := Concatenate<20,2>(stae.c, for i : 5..7 {new_e.carrybits[i]});
  tmpd := ConcatZeros<7,15>(new_w.carrybits);
  d := Concatenate<22,2>(
    tmpd, [Reg(rvprev01.arr_d[RLEN()-2]),Reg(rvprev01.arr_d[RLEN()-1])]);

  Z := Isz(I-15);
  [1-Z, Z] -> (
    RetTuple(stw.a, padb, padc, d, stae.e, stae.f,
             I+1, S2MAJOR_MIX()+S, 0, rvprev01.blk, rvprev01.midx, aux_regs),
    { F := Isz(S-2);
      [1-F, F] -> (
        RetTuple(stw.a, padb, padc, d, stae.e, stae.f,
                 0, S2MAJOR_MIX()+S+1,
                 rvprev01.rnd, rvprev01.blk, rvprev01.midx, aux_regs),
        RetTuple(stw.a, padb, padc, d, stae.e, stae.f,
                 S2MINOR_SKIP()+S2MINOR_FINI(), S2MAJOR_SETUP(),
                 rvprev01.rnd, rvprev01.blk, rvprev01.midx, aux_regs))
    })
}

component sha256mix_all<S: Val>(rvals: RetTuple) {
  m := OneHotU<AUXLEN()>(rvals@1.minor);
  for i : AUXLEN()-AUXBIN()..AUXLEN() { 0 = m[i] * (1-m[i]); };
  r := m -> (sha256mix<S,0>(rvals, m),
             sha256mix<S,1>(rvals, m),
             sha256mix<S,2>(rvals, m),
             sha256mix<S,3>(rvals, m),
             sha256mix<S,4>(rvals, m),
             sha256mix<S,5>(rvals, m),
             sha256mix<S,6>(rvals, m),
             sha256mix<S,7>(rvals, m),
             sha256mix<S,8>(rvals, m),
             sha256mix<S,9>(rvals, m),
             sha256mix<S,10>(rvals, m),
             sha256mix<S,11>(rvals, m),
             sha256mix<S,12>(rvals, m),
             sha256mix<S,13>(rvals, m),
             sha256mix<S,14>(rvals, m),
             sha256mix<S,15>(rvals, m));
  for i : 0..BLEN() { 0 = r.arr_a[i] * (1-r.arr_a[i]); };
  for i : 0..BLEN() { 0 = r.arr_b[i] * (1-r.arr_b[i]); };
  for i : 0..BLEN() { 0 = r.arr_c[i] * (1-r.arr_c[i]); };
  for i : 0..7 { 0 = r.arr_d[i] * (1-r.arr_d[i]); };
  r
}

component sha256init<I: Val>(rvprev01: RetTuple,
                             auxr: Array<NondetReg, AUXLEN()>) {
  packed_a := IVvals(4-I);
  low_a := ToBitsU<16>(packed_a.low);
  FromBits<16>(low_a) = packed_a.low;
  high_a := ToBitsU<16>(packed_a.high);
  FromBits<16>(high_a) = packed_a.high;
  unpacked_a := Concatenate<16,16>(low_a, high_a);
  packed_e := IVvals(8-I);
  low_e := ToBitsU<16>(packed_e.low);
  FromBits<16>(low_e) = packed_e.low;
  high_e := ToBitsU<16>(packed_e.high);
  FromBits<16>(high_e) = packed_e.high;
  unpacked_e := Concatenate<16,16>(low_e, high_e);

  stae := ae2s(unpacked_a, unpacked_e);
  padc := ConcatZeros<20,2>(stae.c);
  Z := Isz(4-I);
  [1-Z, Z] -> (
    RetTuple(dummyB(), dummyB(), padc, dummyE(), stae.e, stae.f,
             S2MINOR_SKIP()+I, S2MAJOR_SETUP(), 0, 0, rvprev01.midx, auxr),
    RetTuple(dummyB(), dummyB(), padc, dummyE(), stae.e, stae.f,
             0, S2MAJOR_LOAD(), 0, 0, rvprev01.midx, auxr))
}

component ShaOutput(vals: Array<Val, 16>) {
  vals := for v : vals { Reg(v) };
}

component sha256fini<I: Val>(rvals: RetTuple,
                             auxr: Array<NondetReg, AUXLEN()>) {
  rvprev01 := rvals@1;
  rvprev02 := rvals@2;
  rvprev03 := rvals@3;
  rvprev04 := rvals@4;
  rvprev68 := rvals@68;

  packed_a := add(pack2(s2a(rvprev04)), pack2(s2a(rvprev68)));
  new_a := unpack_with_carry(packed_a.low, packed_a.high);
  packed_e := add(pack2(s2e(rvprev04)), pack2(s2e(rvprev68)));
  new_e := unpack_with_carry(packed_e.low, packed_e.high);
  stae := ae2s(new_a, new_e);
  tmp14 := Concatenate<7,7>(new_a.carrybits, new_e.carrybits);
  pada := ConcatZeros<14,8>(tmp14);
  padc := ConcatZeros<20,2>(stae.c);

  Z := Isz(8-I);
  moreInput := rvprev01.midx + 1 - rvprev01.blk;
  invMoreInput := NondetReg(Inv(moreInput));
  prod := Reg(moreInput*invMoreInput);
  moreInput*(1-prod) = 0;
  d_suffix := [prod, Reg(rvprev01.arr_d[22]), Reg(rvprev01.arr_d[23])];
  d := Concatenate<21,3>(dummyS(), d_suffix);
  zb := ConcatElem<SLEN()>(dummyS(), invMoreInput);
  [1-Z, Z] -> (
    RetTuple(pada, zb, padc, d, stae.e, stae.f,
             S2MINOR_SKIP()+I, S2MAJOR_SETUP(),
             rvprev01.rnd, rvprev01.blk, rvprev01.midx, auxr),
    { 
      [prod, 1-prod] -> (
        RetTuple(pada, zb, padc, d, stae.e, stae.f,
                 0, S2MAJOR_LOAD(),
                 rvprev01.rnd, rvprev01.blk, rvprev01.midx, auxr),
        { fa := pack2rev(new_a);
          fb := pack2rev(s2a(rvprev01));
          fc := pack2rev(s2a(rvprev02));
          fd := pack2rev(s2a(rvprev03));
          fe := pack2rev(new_e);
          ff := pack2rev(s2e(rvprev01));
          fg := pack2rev(s2e(rvprev02));
          fh := pack2rev(s2e(rvprev03));
          res := [fa.high, fa.low, fb.high, fb.low,
                  fc.high, fc.low, fd.high, fd.low,
                  fe.high, fe.low, ff.high, ff.low,
                  fg.high, fg.low, fh.high, fh.low];
          Log("sha256output: %x,%x %x,%x %x,%x %x,%x %x,%x %x,%x %x,%x %x,%x",
            res[0],  res[1],  res[2],  res[3],
            res[4],  res[5],  res[6],  res[7],
            res[8],  res[9],  res[10], res[11],
            res[12], res[13], res[14], res[15]);
          global output := ShaOutput(res);
          RetTuple(pada, zb, padc, d, stae.e, stae.f,
                   S2MINOR_SKIP()+S2MINOR_OUTPUT(), S2MAJOR_SETUP(),
                   0, 0, 0, auxr)
        })})
}
